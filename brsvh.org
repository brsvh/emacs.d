#+title:Burgess Chang's Emacs Configuration
#+options: author:nil H:4 num:nil tags:t timestamp:nil toc:2
#+export_file_name: README
#+exclude_tags: nonexport
#+startup: overview
:properties:
:header-args: :comment no :lexical t :mkdirp yes :tangle (eval my-profile)
:end:

* Header :nonexport:

#+begin_src emacs-lisp
  ;;; brsvh.el --- Burgess Chang's Profile -*- lexical-binding: t; -*-

  ;; Copyright (C) 2022 Burgess Chang

  ;; Author: Burgess Chang <bsc@brsvh.org>
  ;; Keywords: internal
  ;; Package-Requires: ((emacs "29.0.50"))
  ;; Version: 0.50.0

  ;; This file is part of emacs.d.

  ;; emacs.d is free software: you can redistribute it and/or modify it
  ;; under the terms of the GNU General Public License as published by
  ;; the Free Software Foundation, either version 3 of the License, or
  ;; (at your option) any later version.

  ;; emacs.d is distributed in the hope that it will be useful, but
  ;; WITHOUT ANY WARRANTY; without even the implied warranty of
  ;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
  ;; General Public License for more details.

  ;; You should have received a copy of the GNU General Public License
  ;; along with emacs.d. If not, see <https://www.gnu.org/licenses/>.

  ;;; Commentary:

  ;; Burgess Chang's Emacs configuration.

  ;;; Code:
#+end_src

* TODO About

** TODO Why Emacs?

** TODO Why Vanila Emacs?

** TODO How literature configuration works? 

** TODO Why a monolithic configuration?

** TODO Special Thanks

* Convention

I keep the configuration file style consistent as it grows by conventions for functions, variables, key bindings, and file storage.

** Name Convention

I use a very simple function and variable naming convention.

- Public fucntions and variables should be named start with the prefix ~my-~.
- Private or internal fucntions and variables should be named start with the prefix ~my--~.

** Bind Convention

For all commands, I have created and from other packages, I bind them to *C-c* when I want to call them through a series of key combinations.

I defined a keymap named ~ctl-c-map~, just like ~ctl-x-map~, binding all commands with *C-c* as a prefix key to it.

#+begin_src emacs-lisp
  (defvar ctl-c-map (make-keymap)
    "Default keymap for C-c commands.")

  (keymap-set global-map "C-c" ctl-c-map)
#+end_src

Usually, the major mode command is bound to some key under the prefix key *C-c* and needs to hold down *Control*.  For example, *C-c C-e* is bound to ~elisp-eval-buffer~ in ~emacs-lisp-mode~, *C-c C-l* is bound to ~org-insert-link~ in ~org-mode~, etc.

So I bind the commands I need to the letter keys with *C-c* as the prefix key.  These letter keys are also used for categorization purposes.

#+begin_src emacs-lisp
  (defvar ctl-c-e-map (make-keymap)
    "Default keymap for C-c e commands.")

  (defvar ctl-c-f-map (make-keymap)
    "Default keymap for C-c f commands.")

  (defvar ctl-c-o-map (make-keymap)
    "Default keymap for C-c v commands.")

  (defvar ctl-c-v-map (make-keymap)
    "Default keymap for C-c v commands.")

  (keymap-set ctl-c-map "e" ctl-c-e-map)
  (keymap-set ctl-c-map "f" ctl-c-f-map)
  (keymap-set ctl-c-map "o" ctl-c-o-map)
  (keymap-set ctl-c-map "v" ctl-c-v-map)
#+end_src

These letter keys correspond to the following categories and usages.

- ~e~: commands for Emacs management.
- ~f~: commands for file management.
- ~v~: commands for version control.

** File Convention

The default paths used to store configuration files and persistent data are inconsistent across Emacs packages.  This isn’t just a problem with third-party packages but even with built-in packages.

Some packages put these files directly in ~user-emacs-directory~ or ~$HOME~ or in a subdirectory of either of the two or elsewhere.  Furthermore, file names that don’t provide insight into what package might have created them are sometimes used.

So I manage them myself, following the XDG Base Directory specification as much as possible.  

I have grouped the Emacs-related files into four categories.

- cache file: volatile; created by Emacs Packages.
- config file: non-volatile; created by me; I care about their content.
- data file: non-volatile; created by Emacs Packages; crossing session effective.
- state file: non-volatile; created by Emacs Packages; current session effective.

#+begin_src emacs-lisp :tangle no
  (defvar my-config-directory user-emacs-directory
    "Directory beneath which additional config files are placed.")

  (defvar my-cache-directory (let ((linux '(berkeley-unix
					    gnu
					    gnu/kfreebsd
					    gnu/linux))
				   (xdg (getenv "XDG_CACHE_HOME")))
			       (if (and (memq system-type linux) xdg)
				   (expand-file-name "emacs/" xdg)
				 user-emacs-directory))
    "Directory beneath which additional volatile files are placed.")

  (defvar my-data-directory (let ((linux '(berkeley-unix
					   gnu
					   gnu/kfreebsd
					   gnu/linux))
				  (xdg (getenv "XDG_DATA_HOME")))
			      (if (and (memq system-type linux) xdg)
				  (expand-file-name "emacs/" xdg)
				user-emacs-directory))
    "Directory beneath which additional non-volatile files are placed.")

  (defvar my-state-directory (let ((linux '(berkeley-unix
					   gnu
					   gnu/kfreebsd
					   gnu/linux))
				  (xdg (getenv "XDG_STATE_HOME")))
			      (if (and (memq system-type linux) xdg)
				  (expand-file-name "emacs/" xdg)
				user-emacs-directory))
    "Directory beneath which additional state files are placed.")
#+end_src

** Supported Environment

My operating system is GNU/Linux, and my distribution is Arch Linux.  My Emacs configuration always lives in the master branch of the Emacs code repository.

Current verbose build information about my Emacs:

#+name: emacs-build-description
#+begin_src emacs-lisp :exports results :tangle no
  (with-temp-buffer (emacs-build-description) (buffer-string))
#+end_src

The Emacs version below 29.0.50 will have some errors during startup, so this configuration only works on the master branch.  Likewise, I haven't adapted and tested it for Windows and macOS, so it may work.

* Booting Up

Let's make some necessary settings to facilitate our further personalizing Emacs.

** Early Init

After Emacs 27.0.50, Emacs supports be configured before eval ~user-init-file~.  I did not abuse the early initialization.  I configured some options for ~comp~, ~package~, and default frame in the early initialization.

*** Move Emacs Native Compilation cache to not mess up my emacs.d

Beginning from Emacs 28.0.50, Emacs support native compilation.  It greatly improves the loading speed of Emacs Lisp.  It is configuration free, but I need to move default /eln-cache// directory under ~user-emacs-directory~ to follow my [[*File Convention][File Convention]].

#+begin_src emacs-lisp :tangle no
  (startup-redirect-eln-cache (concat my-cache-directory "eln-cache/"))
#+end_src

** Preparation for literature configuration

I used org-mode to write the literature configuration(this file).  It supports extracting code blocks to code files and exporting to other file formats.

The actual loading sequence of the literature configuration is as follows:

1. Eval init file
2. Try to extract code blocks to a configuration file(Emacs Lisp file)
3. Try to eval the configuration file

To try to extract the code block to the configuration file, I declared a function that compares the update time of the literature configuration file and the configuration file to determine if it needs to be extracted.

#+begin_src emacs-lisp :tangle no
  (defun my-profile-publish (file target-file)
    "Try to export code from the literate FILE to TARGET-FILE.

  Optional argument TARGET-FILE can be used to specify a default
  export file for all source blocks.

  Return a list whose CAR is the tangled file name, and CDR is t if
  FILE is tangled, otherwise nil."
    (unless (fboundp 'org-babel-tangle-file)
      (autoload 'org-babel-tangle-file "ob-tangle"))
    (eval-when-compile
      (declare-function org-babel-tangle-file "ob-tangle"))
    (if (or (not (file-exists-p file))
	    (file-newer-than-file-p my-literate-profile my-profile))
	(cons (car (org-babel-tangle-file file target-file)) t)
      (cons target-file nil)))
#+end_src

Then the literature configuration can be loaded by loading the configuration file.

#+begin_src emacs-lisp :tangle no
  (defvar my-literate-profile (concat user-emacs-directory "brsvh.org")
    "Burgess Chang's literate configuration profile.")

  (defvar my-profile (concat my-data-directory "lisp/brsvh.el")
    "Burgess Chang's configuration profile.")

  (load (car (my-profile-publish my-literate-profile my-profile)) nil t)
#+end_src

*Notice*: the above two code blocks are not extracted into the configuration file, they actually exist in the ~user-init-file~.

** Package Management

I choose to use the built-in /package.el/ because it provides by Emacs distribution and is almost configuration free.

It can be used with a simple ~require~, and all installed packages will be activated.

#+begin_src emacs-lisp
  (require 'package)
#+end_src

Beginning at Emacs 27.0.50, package activation occurs before ~user-init-file~ is loaded but after ~early-init-file~.  So I no longer need to do package activation.  Only when the package activations failed I do it.

#+begin_src emacs-lisp
  (unless package--activated
    (package-activate-all))
#+end_src

As mentioned above, I need to put the customization of /package.el/ that affect package initialization to ~early-init-file~.  In fact, I did redirect ~package-user-dir~ in the ~early-init-file~.

#+begin_src emacs-lisp :tangle no
  ;; Beginning at Emacs 27.0.50, package initialization occurs before
  ;; `user-init-file' is loaded, but after `early-init-file', so some of
  ;; necessary options of `package' I need to set in `early-init-file'.
  (setq package-user-dir (format "%s%s/"
				 (concat my-cache-directory "elpa/")
				 emacs-version)
	package-gnupghome-dir (concat package-user-dir "gnupg/")
	;; Enable native ompilation support of packages.
	package-native-compile t
	;; Enable precompute activation actions to speed up package
	;; activatation.
	package-quickstart t
	package-quickstart-file (concat package-user-dir "loaddefs.el"))
#+end_src

*** Emacs Lisp Package Archive

By default, Emacs support downloading and installing ELPA from official archives sites, aka GNU ELPA and Non-GNU ELPA.  But these two archive repositories contain only a relatively small part of the Emacs ecosystem, so I add Milkypostman’s ELPA for convenience get more and more packages.

#+begin_src emacs-lisp
  (add-to-list 'package-archives
	       (cons "melpa" (format "http%s://melpa.org/packages/"
				     (if (gnutls-available-p) "s" "")))
	       'append)
#+end_src

*** Easy-to-use package configuration macro

In the real world of Emacs configuration, you will find many ~with-eval-after-load~, ~add-hook~, ~require~ and other blocks of Emacs Lisp.  This will seem dizzying and somewhat lengthy to write.

In the ELPA repositories, you can find several packages designed to simplify the configuration of the Emacs Lisp package by providing the macro wrapper.

The packages are well known are:

- [[https://github.com/jwiegley/use-package][use-package.el]]
- [[https://github.com/conao3/leaf.el][leaf.el]]
- [[https://git.sr.ht/~pkal/setup][setup.el]]

I choose to use [[https://github.com/jwiegley/use-package][use-package.el]], because of its long maintenance history and more users.

All that needs to be done is to ensure it has been installed from ELPA repositories, and require it.

#+begin_src emacs-lisp
  (unless (package-installed-p 'use-package)
      (unless (memq 'use-package package-archive-contents)
	(package-refresh-contents))
      (package-install 'use-package))
#+end_src

If interested, you can further read follow references to learn about managing the Emacs Lisp package with other packagers.

- [[https://github.com/radian-software/straight.el#comparison-to-other-package-managers][Comparison to other package managers]]
- [[https://systemcrafters.cc/advanced-package-management/using-straight-el/][Advanced Emacs Package Management with straight.el]]
  
**** Keyword conventions for ~use-package~

I will not use some keywords for the following reasons.

- ~:bind~, ~:bind*~, ~:bind-keymap~ and ~:bind-keymap*~

  These keywords actually call the functions ~bind-key~ and ~bind-keys~ provided by [[https://github.com/jwiegley/use-package/blob/master/bind-key.el][bind-key.el]].  The [[https://github.com/jwiegley/use-package/blob/master/bind-key.el][bind-key.el]] is, as of now (October 2022), based on the ~define-key~, which has been deprecated since Emacs 29.0.50.  And it also has a lousy indentation.  For these reasons, I refused to use these keywords.  Instead, I use ~keymap-set~ to do the key binding.

- ~:custom~

  I use ~setq~ everywhere.

- ~:custom-face~

  ~:custom-face~ keyword eval ~custom-set-faces~ before ~require~ feature.  Sometimes I need set faces after require, so ~set-face-attribute~ is more flexible.

**** Deferred loading conventions for ~use-package~

[[https://github.com/jwiegley/use-package][use-package.el]] provides ~:defer~ keyword to control block to defer loading, which means eval the code of the ~use-package~ block after the specified feature has been loaded.  The ~:defer~ keyword is used to explicit emphasis defer loading.  In addition, the ~:commands~ and ~:autoload~ keywords will also implicitly defer loading by default.  So in this configuration, I will not use the ~:defer~ keyword explicitly in cases where it is not necessary.

**** Configure ~use-package~ how to expand

When using the ~:hook~ keyword to handle something to the hook, [[https://github.com/jwiegley/use-package][use-package.el]] will use an omitted hook name.  For example, ~c-mode-hook~ omitted as ~c-mode~, ~emacs-startup-hook~ omitted as ~emacs-startup~.  I'm a bit careless, and sometimes I mess things up.  So make [[https://github.com/jwiegley/use-package][use-package.el]] always use the proper hook name.

#+begin_src emacs-lisp
  (use-package use-package
    :defer t
    :init
    (setq use-package-hook-name-suffix nil))
#+end_src

*** Get package from source

For packages that are not included in ELPA, I can only download them locally to use it.  But for such packages that already have version control, I manage them through quelpa.

#+begin_src emacs-lisp
  (use-package quelpa
    :ensure t
    :pin melpa
    :autoload quelpa-setup-p
    :init
    (setq quelpa-dir (format "%s%s/"
			     (concat my-cache-directory "quelpa/")
			     emacs-version))

    ;; Inhibit `quelpa' update MELPA's recipes every startup.
    (setq quelpa-update-melpa-p nil)

    ;; Inhibit `quelpa' auto-update.
    (setq quelpa-upgrade-p nil)

    ;; Inhibit `quelpa' upgrade itself.
    (setq quelpa-self-upgrade-p nil) 

    ;; Initialize `quelpa'.
    (quelpa-setup-p))
#+end_src

**** Extend keyword for Easy-to-use package configuration macro

I use a keyword named ~:quelpa~ in ~use-package~ macro, it implemented by [[https://github.com/quelpa/quelpa-use-package][quelpa-use-package.el]].

#+begin_src emacs-lisp
  (use-package quelpa-use-package
    :ensure t
    :pin melpa
    :after quelpa
    :init
    (require 'quelpa-use-package))
#+end_src

*** Update Packages

Once I have installed the packages, a new question is, how do I update them?

The interactive approach is to call ~list-packages~, press ~U x~ in the ~*Package*~ buffer, and then confirm according to the prompt to update all packages.  ~U~ means mark all upgradable packages, ~x~ means perform installation of marked packages.

It works, but...not satisfied.

So I use [[https://github.com/rranelli/auto-package-update.el][auto-package-update.el]] to get further simplification and automation.

#+begin_src emacs-lisp
  (use-package quelpa
    :ensure t
    :pin melpa
    :commands
    (quelpa-upgrade-all
     quelpa-upgrade-all-maybe))

  (use-package auto-package-update
    :ensure t
    :pin melpa
    :commands
    (auto-package-update-maybe auto-package-update-now)
    :init
    (defun my-auto-package-update-now ()
      "Update installed Emacs packages.

  Update all packages that installed from `package' and `quelap'."
      (interactive)
      (auto-package-update-now)
      (quelpa-upgrade-all))

    (keymap-set ctl-c-e-map "u" 'my-auto-package-update-now)

    ;; Move `auto-package-update' status file.
    (setq auto-package-update-last-update-day-path
	  (expand-file-name "last-update" my-state-directory))

    ;; Delete residual old versions
    (setq auto-package-update-delete-old-versions t)

    ;; Do not bother me when updates have taken place.
    (setq auto-package-update-hide-results t)

    ;; Update installed packages at startup if there is an update
    ;; pending.
    (auto-package-update-maybe)
    (quelpa-upgrade-all-maybe))
#+end_src

** Inhibit saving customization to ~user-init-file~ by appending

Although I do not use the *Easy Customization Interface* of Emacs, some packages will use it somewhere I do not notice.

I want to keep the content of ~user-init-file~ and my configuration file not auto-generated, so I choose to save customizations somewhere other than my initialization file.

#+begin_src emacs-lisp
  (use-package cus-edit
    :defer t
    :preface
    (setq custom-file (expand-file-name "custom.el" my-state-directory))
    :when (file-exists-p custom-file)
    :init
    (load custom-file nil 'nomessage))
#+end_src

** Syncing to the shell's environment variables

For one reason or another, an Emacs instance begun from the desktop may miss some PATH set in the interactive shell.  Thus, it is challenging to use utilities installed by ~cargo~.  I use an excellent package named [[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell.el]] written by Steve Purcell to solve this problem, which will synchronize PATH from the shell.

#+begin_src emacs-lisp
  (use-package exec-path-from-shell
    :ensure t
    :pin melpa
    :when (memq window-system '(x pgtk))
    :hook
    (emacs-startup-hook . exec-path-from-shell-initialize))
#+end_src

** Intelligent garbage collection

Emacs uses a fixed threshold to trigger Garbage Collection by default, which is not flexible and may cause performance degradation by triggering Garbage Collection frequently.  I use a sneaky Garbage Collection strategy to minimize Garbage Collection interference with user activity.  It is provide by [[https://gitlab.com/koral/gcmh][gcmh.el]], the strategy will use a low Garbage Collection threshold when idling, and set a high threshold during normal.

#+begin_src emacs-lisp
  (use-package gcmh
    :ensure t
    :pin gnu
    :hook (emacs-startup-hook . gcmh-mode))
#+end_src

** UTF-8 everywhere

In the Linux world, there is a silent agreement that UTF-8 is the best encoding to use for Unicode.  So I use utf8 by default, unless I explicitly specify the encoding.

#+begin_src emacs-lisp
  (use-package mule-cmds
    :commands
    (set-language-environment
     set-default-coding-systems
     prefer-coding-system)
    :init
    (set-language-environment "utf-8")
    (set-default-coding-systems 'utf-8)
    (prefer-coding-system 'utf-8))
#+end_src

** Using Emacs as a server

When I invoke Emacs at other places, I want to share buffers, a command history, or additional information with the existing Emacs process.  So it is necessary to make Emacs start as a daemon.

#+begin_src emacs-lisp
  (use-package server
    :autoload server-running-p
    :config
    (setq server-auth-dir
	  (expand-file-name "server/" my-state-directory))
    :hook
    ;; Start server at startup.
    (emacs-startup-hook
     . (lambda ()
	 (eval-when-compile (require 'server))
	 (unless (server-running-p) (server-start)))))
#+end_src

** Resarting Emacs

When I change the configuration file, and in some other cases, I want to be able to restart Emacs instead of turning it off and on again.

#+begin_src emacs-lisp
  (use-package restart-emacs
    :ensure t
    :pin melpa
    :commands restart-emacs
    :init
    (keymap-set ctl-c-e-map "r" 'restart-emacs))
#+end_src

* Common Library

There are some shared libraries need to setup, mainly related to file of libraries saving.

[[https://github.com/rougier/svg-lib][svg-lib.el]] is used to provides a lot of icons in *svg* format, it will save the cache of icons to a directory under ~user-emacs-directory~.  Make it follow my [[*File Convention][File Convention]].

#+begin_src emacs-lisp
  (use-package svg-lib
    :ensure t
    :pin gnu
    :defer t
    :config
    (setq svg-lib-icons-dir
	  (expand-file-name "svg-lib/" my-cache-directory)))
#+end_src

[[https://github.com/magit/transient][transient.el]] implements abstraction involving a prefix command, infix arguments and suffix commands, it used to create interface for command-line program.  It will save levels, values, and history to a directory named /transient/ under ~user-emacs-directory~, make it follow my [[*File Convention][File Convention]].

#+begin_src emacs-lisp
  (use-package transient
    :ensure t
    :pin melpa
    :defer t
    :config
    (setq transient-history-file
	  (expand-file-name "transient/history.el" my-state-directory)
	  transient-levels-file
	  (expand-file-name "transient/levels.el" my-state-directory)
	  transient-values-file
	  (expand-file-name "transient/values.el" my-state-directory)))
#+end_src

* Appearance

The default interface of Emacs is old-fashioned and old-school, and I'm a young person, so I want it to be more modern.  I used a more luminous Theme and Mode Line.

** Change the default frame layout

Emacs support work on graphical and non-graphical frames.  Frame layout will show a menu bar, tool bar, and vertical scroll bar by default.  The exception is the tool bar and vertical scroll bar are not displayed in the character-only frame.

Although the menu bar, tool bar, and scroll bar are practical, my operation flow is purely keyboard-based.  For me, they are rarely used and distract me, so I disable them.

Another unimportant reason is that they are initialized before eval the init file(after ~before-init-hook~), which slow down Emacs startup.  Emacs will also get speedup by disabling them.

I handle anonymous functions to disable these bars to ~emacs-startup~hook~.

#+begin_src emacs-lisp :tangle no
  (use-package menu-bar
    :defer t
    :hook
    (emacs-startup-hook . (lambda () (menu-bar-mode -1))))

  (use-package tool-bar
    :defer t
    :hook
    (emacs-startup-hook . (lambda () (tool-bar-mode -1))))

  (use-package scroll-bar
    :defer t
    :hook
    (emacs-startup-hook . (lambda () (scroll-bar-mode -1))))
#+end_src

*** Change the default frame layout during early initialization

If you notice that Emacs enable bars after ~before-init-hook~, I disable bars at ~emacs-startup-hook~ in the previous paragraph.

This looks strange, so I turn them off by setting ~default-frame-alist~ in the ~early-init-file~.

#+begin_src emacs-lisp :tangle no
  ;; Add some essentia layout parameters of frame to preset values, and
  ;; ensure the minor modes corresponding to follow values are disabled.
  (push (cons 'menu-bar-lines nil) default-frame-alist)
  (push (cons 'tool-bar-lines nil) default-frame-alist)
  (push (cons 'vertical-scroll-bars nil) default-frame-alist)
  (push (cons 'horizontal-scroll-bars nil) default-frame-alist)
  (setq-default menu-bar-mode nil tool-bar-mode nil scroll-bar-mode nil)
#+end_src

** Change the default startup screen

Emacs will display *GNU Emacs* buffer that is not useful.  I use *scratch* buffer as default screen and clean the content of it and echo area.

#+begin_src emacs-lisp
  (use-package startup
    :no-require t
    :init
    (setq inhibit-startup-screen t
	  inhibit-startup-echo-area-message t
	  initial-scratch-message nil
	  initial-major-mode 'fundamental-mode))
#+end_src

** Resize pixelwise

I preferred resize frame and window pixelwise rather linewise.

#+begin_src emacs-lisp
  (use-package emacs
    :no-require t
    :init
    ;; Resize windows pixelwise.
    (setq window-resize-pixelwise t)

    ;; Resize frame pixelwise.
    (setq frame-resize-pixelwise t))
#+end_src

** Accessible themes

Emacs' default theme leaves much to be desired: It does not look sleek and shiny, which usually leaves first-timers with a poor, shallow impression of the system.  Below I set up a theme that makes Emacs look accessible.

#+begin_src emacs-lisp
  (use-package modus-themes
    :ensure t
    :pin gnu
    :autoload
    (modus-themes-load-themes
     modus-themes-load-operandi
     modus-themes-load-vivendi)
    :init
    (setq modus-themes-bold-constructs t
	  modus-themes-italic-constructs t
	  modus-themes-subtle-line-numbers t
	  modus-themes-inhibit-reload t
	  modus-themes-fringes 'nil
	  modus-themes-mode-line '(borderless moody)
	  modus-themes-markup '(background italic)
	  modus-themes-links '(italic neutral-underline)
	  modus-themes-prompts '(intense bold)
	  modus-themes-headings
	  '((0 . (rainbow 1.5))
	    (1 . (rainbow 1.3))
	    (2 . (rainbow 1.2))
	    (3 . (rainbow 1.1))
	    (t . (rainbow))))
    (modus-themes-load-themes)
    :config
    (modus-themes-load-operandi)
    ;; Set more subtle line numbers.
    (when (member 'modus-operandi custom-enabled-themes)
      (let ((bg (face-attribute 'modus-themes-hl-line :background)))
	(set-face-attribute 'line-number-current-line
			    nil
			    :background bg))))
#+end_src

If you look at the themes built into the Emacs distribution, you will see that /modus-themes.el/ are included in the emacs distribution.  But I always get the latest theme from ELPA.

*** Show subtle line numbers

In Modus Themes, their background colors are not uniform when both ~display-line-numbers-mode~ and ~hl-line-mode~ are activated.  I have them displaying the same background color, which looks more subtle.

#+begin_src emacs-lisp
  (use-package modus-themes
    :ensure t
    :pin gnu
    :config
    (let ((bg (face-attribute 'modus-themes-hl-line :background)))
      (set-face-attribute 'line-number-current-line nil :background bg)))
#+end_src

** Tabs and Ribbons style Mode Line

The Mode Line is a part near the bottom of Emacs that gives information about the current buffer, such as encoding, buffer size, cursor position, major mode and minor mode.

I use [[https://github.com/tarsius/moody][moody.el]] to get a tabs and ribbons style mode line.  Its advantage is only making modest changes to the mode line, adding or replacing only a few elements.

#+begin_src emacs-lisp
  (use-package moody
    :ensure t
    :pin melpa
    :autoload
    (moody-replace-mode-line-buffer-identification
     moody-replace-vc-mode
     moody-replace-eldoc-minibuffer-message-function)
    :init
    ;; Replace elements of Mode Line.
    (moody-replace-mode-line-buffer-identification)
    (moody-replace-vc-mode)
    (moody-replace-eldoc-minibuffer-message-function))

  (use-package emacs
    :no-require t
    :init
    ;; Inhibit draw the underline at the same place as the descent line.
    (setq x-underline-at-descent-line t))
#+end_src

*** Replace Minor Modes with a menu button

When a buffer activates so many minor modes, the Mode Line will show many names of minor modes.  It isn't charming because usually too long mode info messages can cause other information to be outside the scope of the frame.

I use [[https://github.com/tarsius/minions][minions.el]] to replace the minor modes list with a menu button that lists enabled minor modes.

#+begin_src emacs-lisp
  (use-package minions
    :ensure t
    :pin melpa
    :defines minions-prominent-modes
    :init
    ;; Show button as :)
    (setq minions-mode-line-lighter ":)"
	  minions-mode-line-delimiters '("" . ""))
    :hook (emacs-startup-hook . minions-mode))
#+end_src

~minions-prominent-modes~ is used to show minor mode directly in the mode line.

** Context Menu

Emacs support clicking the mouse button activates the menu whose contents depends on its surrounding context.  But it is disabled by default, sometimes it helps me, so I activated it at startup.

#+begin_src emacs-lisp
  (use-package mouse
    :hook (emacs-startup-hook . context-menu-mode))
#+end_src

** Smooth Scrolling

smooth scrolling, as its name indicates, is a feature that allows you to scroll smoothly. The default scrolling is a bit choppier, so I use ~pixel-scroll-mode~.

#+begin_src emacs-lisp
  (use-package pixel-scroll
    :hook
    ((emacs-startup-hook . pixel-scroll-mode)
     (emacs-startup-hook . pixel-scroll-precision-mode)))
#+end_src

** Highlighting changes

Highlighting uncommitted changes on the fringe.

#+begin_src emacs-lisp
  (use-package diff-hl
    :ensure t
    :pin gnu
    :hook
    (find-file-hook . diff-hl-mode)
    (vc-dir-mode-hook  . diff-hl-dir-mode)
    (dired-mode-hook   . diff-hl-dired-mode)
    (diff-hl-mode-hook . diff-hl-flydiff-mode))
#+end_src

* Completion

Emacs provides a *Completion* feature that fills in the rest of a name starting from an abbreviation for it.  I usually fill in two types of Emacs when I need to fill in.

- *input completion*: completion of my input in the minibuffer
- *text completion*: completion of words or abbreviations in a buffer.

When I fill in both types of content, I usually want them to have different interfaces.  I prefer input completions to show candidates in the minibuffer, while text completions provide a pop-up menu in place.

There are so many input completion packages in ELPA repositories, available for me to choose from [[https://github.com/emacs-helm/helm][helm.el]], [[https://github.com/abo-abo/swiper][ivy.el]], [[https://github.com/radian-software/selectrum][selectrum.el]], [[https://github.com/minad/vertico][vertico.el]], etc.

I have used all these packages for a long or short time.  I choose to use [[https://github.com/minad/vertico][vertico.el]] finally.  Because it achieves full compatibility with built-in Emacs completion commands and completion tables by reusing the built-in facilities system.

And for text completion, less choice, [[https://github.com/company-mode/company-mode][company.el]] and [[https://github.com/minad/corfu][corfu.el]].  Because [[https://github.com/minad/corfu][corfu.el]] relies exclusively on the standard Emacs completion API, [[https://github.com/company-mode/company-mode][company.el]] defines its own API for the backends.  So I use [[https://github.com/minad/corfu][corfu.el]] other than [[https://github.com/company-mode/company-mode][company.el]].

** Allow recursive edit in minibuffer

By default, Emacs do not allow a new minibuffer in the current minibuffer.  It inconveniences many everyday tasks, such as viewing the documentation for a command before executing it or invoking the shell command ~date~ when calling ~find-file~ to create a file containing the current date.

I set ~enable-recursive-minibuffers~ to ~t~ to allow use recursive minibuffer.

#+begin_src emacs-lisp
  (use-package emacs
    :no-require t
    :init
    (setq enable-recursive-minibuffers t))
#+end_src

** Hide commands in M-x which do not work in the current mode

My Emacs contains thousands of commands, and I want to hide some commands that are not available in the current main mode to avoid interfering with my command completion.

In particular, note that this only works on Emacs after 28.0.50.

#+begin_src emacs-lisp
  (use-package simple
    :defer t
    :config
    (setq read-extended-command-predicate
	  #'command-completion-default-include-p))
#+end_src

** Do not allow the cursor in the minibuffer prompt

Keep cursor outside of any ~cursor-intangible~ text property.

#+begin_src emacs-lisp
  (use-package emacs
    :no-require t
    :init
    (setq minibuffer-prompt-properties
	  '(read-only t cursor-intangible t face minibuffer-prompt)))

  (use-package cursor-sensor
    :hook
    (minibuffer-setup-hook . cursor-intangible-mode))
#+end_src

** Eye-catching indicator when calling ~completing-read-multiple~

Add prompt indicator when calling ~completing-read-multiple~, it will display ~[CRM<separator>]~, e.g., ~[CRM,]~ if the separator is a comma.

#+begin_src emacs-lisp
  (use-package crm
    :defer t
    :config
    (define-advice completing-read-multiple
	(:filter-args (args) my-crm-indicator)
      "Parsing ARGS and add my prompt indicator.

  \(fn PROMPT TABLE &optional PREDICATE REQUIRE-MATCH INITIAL-INPUT HIST DEF INHERIT-INPUT-METHOD)"
      (cons (format "[CRM%s] %s"
		    (replace-regexp-in-string
		     "\\`\\[.*?]\\*\\|\\[.*?]\\*\\'" ""
		     crm-separator)
		    (car args))
	    (cdr args))))
#+end_src

** Match completion with space

Emacs supports the set of completion styles, with these three types enabled by default: ~basic~, ~emacs22~, and ~partial-completion~.

They are corresponding to:

- basic: completion of the prefix before point and the suffix after point.
- emacs22: prefix completion that only operates on the text before point.
- partial-completion: completion of multiple words, each one taken as a prefix.

For example, if there is a command ~foo-bar~, it could be complete with these inputs:

- ~f_b~, complete by basic style
- ~f_r~, complete by emacs22 style
- ~f-_a~, complete by partial-completion style.

~_~ is mean the position of cursor.

Although the default is sufficient, I would like to be allowed to complete the input with spaces as separators.  So  I use [[https://github.com/oantolin/orderless][orderless]], which provides a completion style named ~orderless~ that divides the pattern into space-separated components and match them.

#+begin_src emacs-lisp
  (use-package minibuffer
    :ensure orderless
    :pin gnu
    :init
    ;; Use orderless style get more flexible completions.
    (setq completion-styles '(substring orderless basic)
	  completion-category-overrides
	  '((file (styles basic partial-completion)))))
#+end_src

Additionally, enable ~partial-completion~ for file path expansion.  ~partial-completion~ is vital for file wildcard support.  Multiple files can be opened at once with ~find-file~ if the input has a wildcard.

** Use vertical completion UI with [[https://github.com/minad/vertico][vertico.el]]

[[https://github.com/minad/vertico][vertico.el]] provides a minor mode named ~vertico-mode~ (Vertico Mode), which will wrap ~completing-read~ to set a vertical UI after activate it.

*Vertico Mode* is easy to use.  It just needs to be activated.  I handle it to ~emacs-startup-hook~ for auto-activated at startup.

#+begin_src emacs-lisp :tangle no
  (use-package vertico
    :ensure t
    :pin gnu
    :hook
    (emacs-startup-hook . vertico-mode))
#+end_src

After activating Vertico Mode, it will display a fixed height minibuffer when completing.  I preferred to use an adjustable height based on the remaining candidates.

Another point needs to be tweaked.  When I go to the last candidate, I need help getting back to the top quickly. I enable cycling of ~vertico-next~ and ~vertico-previous~.

#+begin_src emacs-lisp
  (use-package vertico
    :ensure t
    :pin gnu
    :config
    ;; Grow and shrink the Vertico minibuffer
    (setq vertico-resize t)

    ;; enable cycling for `vertico-next' and `vertico-previous'.
    (setq vertico-cycle t)
    :hook
    (emacs-startup-hook . vertico-mode))
#+end_src

*** Ido-like directory navigation

~find-file~ will open the current directory of files by default, so editing the path of ~find-file~ is a widespread operation, and by default, I can only delete one character at a time. That is too awful and will wear out my patience. [[https://github.com/minad/vertico][vertico.el]] has an extension named ~vertico-directory~, which provides some commands to delete multiple characters in the path at once by word.  I bind these commands to backspace.

#+begin_src emacs-lisp
  (use-package vertico-directory
    :ensure vertico
    :pin gnu
    :after vertico
    :autoload
    (vertico-directory-enter
     vertico-directory-delete-char
     vertico-directory-delete-word)
    :init
    (keymap-set vertico-map "<return>" 'vertico-directory-enter)
    (keymap-set vertico-map "<backspace>" 'vertico-directory-delete-char)
    (keymap-set vertico-map "M-<backspace>" 'vertico-directory-delete-word)
    :hook
    ;; Tidy shadowed file names.
    (rfn-eshadow-update-overlay-hook . vertico-directory-tidy))
#+end_src

*** Use Mouse select candidate

I occasionally use the mouse, and this is why I want to use the mouse to select the candidates.

#+begin_src emacs-lisp
  (use-package vertico-mouse
    :ensure vertico
    :pin gnu
    :after vertico
    :hook
    (vertico-mode-hook . vertico-mouse-mode))
#+end_src

** Completion Overlay Region FUnction

I use [[https://github.com/minad/corfu][corfu.el]] to pop up the completion of text at a point.

#+begin_src emacs-lisp :tangle no
  (use-package corfu
    :ensure t
    :pin gnu
    :commands corfu-mode)
#+end_src

Because I avoid using too many global modes and only complete text when programming, ~corfu-mode~ is handled in the hook of the programming major mode.  As you see, I autoload the command and do not activate it.

*Corfu Mode* works as a wrapper of ~complete-symbol~, so I need press ~C-M-i~ to do complete.  Let it pop up the completion menu automatically.

#+begin_src emacs-lisp :tangle no
  (use-package corfu
    :ensure t
    :pin gnu
    :commands corfu-mode
    :config
    (setq corfu-auto t
	  corfu-quit-no-match 'separator))
#+end_src

*Corfu Mode* jumping between candidates with ~corfu-perivous~ and ~corfu-next~.  When I got to the last candidate, I wanted the next one to return to the first one, so I activated cycling for ~corfu-perivous~ and ~corfu-next~.

#+begin_src emacs-lisp :tangle no
  (use-package corfu
    :ensure t
    :pin gnu
    :commands corfu-mode
    :config
    ;; Enable auto completion
    (setq corfu-auto t
	  corfu-quit-no-match 'separator)

    ;; Enable cycling for `corfu-next' and `corfu-previous'.
    (setq corfu-cycle t))
#+end_src

When I selected an incorrect candidate and quit completion, it has been fill in the point.  It will waste my time to delete it, so I prevent candidate preselection.

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :pin gnu
    :commands corfu-mode
    :config
    ;; Enable auto completion
    (setq corfu-auto t
	  corfu-quit-no-match 'separator)

    ;; Enable cycling for `corfu-next' and `corfu-previous'.
    (setq corfu-cycle t)

    ;; Disable candidate preselection.
    (setq corfu-preselect-first nil))
#+end_src

*** Tab style completion

Complete text by just pressing the ~TAB~ key is a more efficient operation.

#+begin_src emacs-lisp
  (use-package corfu
    :defer t
    :config
    ;; TAB-style keybindings.
    (keymap-set corfu-map "<tab>" 'corfu-next)
    (keymap-set corfu-map "<backtab>" 'corfu-previous))
#+end_src

*** Work in character-only frame

[[https://github.com/minad/corfu][corfu.el]] was created on the top of ~posframe~, so it only works in the graphical frame.  Sometimes I will use Emacs in the character-only frame (terminal).  I need help to complete the text at this time.  Unsurprisingly, the powerhouse Emacs community already has a ready-made package named [[https://codeberg.org/akib/emacs-corfu-terminal][corfu-terminal.el]].

This package provides a global minor mode named ~corfu-terminal-mode~ to replace ~posframe~ with ~popup~.  The only drawback is that it is global, I rewrite it to a minor mode.  The only drawback is that it is global.  I have rewritten it to a non-global minor mode.

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :pin gnu
    :autoload
    (corfu--popup-show
     corfu--popup-hide
     corfu--popup-support-p))

  (use-package corfu-terminal
    :ensure t
    :pin nongnu
    :unless (display-graphic-p)
    :autoload (corfu-terminal--popup-show
	       corfu-terminal--popup-hide
	       corfu-terminal--popup-support-p)
    :preface
    (define-minor-mode my-corfu-terminal-mode
      "Corfu popup on terminal."
      :global nil
      :group 'corfu-terminal
      (if my-corfu-terminal-mode
	  (progn
	    (advice-add #'corfu--popup-show :around
			#'corfu-terminal--popup-show)
	    (advice-add #'corfu--popup-hide :around
			#'corfu-terminal--popup-hide)
	    (advice-add #'corfu--popup-support-p :override
			#'corfu-terminal--popup-support-p))
	(advice-remove #'corfu--popup-show #'corfu-terminal--popup-show)
	(advice-remove #'corfu--popup-hide #'corfu-terminal--popup-hide)
	(advice-remove #'corfu--popup-support-p
		       #'corfu-terminal--popup-support-p)))
    :hook
    (corfu-mode-hook . my-corfu-terminal-mode))
#+end_src

Now I can complete text in terminal.

*** Remember selected candidates and to improve sorting

When I write code in an actual project, some functions or variables need to be completed more often than others. So I would like *Corfu Mode* can do this based on the frequency of use.

#+begin_src emacs-lisp
  (use-package corfu-history
    :ensure corfu
    :pin gnu
    :hook
    (corfu-mode-hook . corfu-history-mode))
#+end_src

*** Show documentation when select candidate

[[https://github.com/galeo/corfu-doc][corfu-doc.el]] provides a minor mode that display a documentation popup for completion candidate when using *Corfu Mode*, it helped me tremendously to complete the text.

#+begin_src emacs-lisp
  (use-package corfu-doc
    :ensure t
    :pin melpa
    :after corfu
    :config
    (keymap-set corfu-map "M-d" 'corfu-doc-toggle)
    (keymap-set corfu-map "M-n" 'corfu-doc-scroll-up)
    (keymap-set corfu-map "M-p" 'corfu-doc-scroll-down)
    :hook (corfu-mode-hook . corfu-doc-mode))
#+end_src

*** Completion in the miniubuffer

I frequently use ~eval-expression~ (~M-:~) to temporary eval code, make it can be complete with *Corfu Mode*.

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :pin gnu
    :preface
    (defun my-corfu-in-minibuffer ()
      "Enable Corfu in the minibuffer if `completion-at-point' is bound."
      (when (where-is-internal #'completion-at-point
			       (list (current-local-map)))
	(corfu-mode +1)))
    :hook
    (minibuffer-setup-hook . my-corfu-in-minibuffer))
#+end_src

*** Beautify with identifiable icon

Some functions and variables are named similarly when writing code, and I need to distinguish them visually.  I use [[https://github.com/jdtsmith/kind-icon][kind-icon.el]] to add some colorful icon before candidates.

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :pin gnu
    :defines corfu-margin-formatters)

  (use-package kind-icon
    :ensure t
    :pin gnu
    :after corfu
    :autoload kind-icon-margin-formatter
    :init
    (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter)
    :config
    ;; Compute blended backgrounds correctly.
    (setq kind-icon-default-face 'corfu-default))
#+end_src

** Show mariginalia of completions

I use [[https://github.com/minad/marginalia][marginalia.el]] get more helpful infomation of completion.

#+begin_quote
[[https://en.wikipedia.org/wiki/Marginalia][Marginalia]] are marks or annotations placed at the margin of the page of a book or in this case helpful colorful annotations placed at the margin of the minibuffer for your completion candidates.[fn:1]
#+end_quote

#+begin_src emacs-lisp
  (use-package marginalia
    :ensure t
    :pin gnu
    :config
    ; Make marginalia align to right.
    (setq marginalia-align 'right)
    :hook
    (emacs-startup-hook . marginalia-mode))
#+end_src

* Actions in Emacs

For me, there are some commands in Emacs that I consider to actions.  Such as opening the URL in a browser, switching to other buffers, searching for something, and operating them.  So I need quickly find these actions and enhance them for me.

** Save actions history

When I do some actions and quit Emacs later, the history of actions will be lost.  I have to retype everything, and it upsets me.  So I activate ~savehist-mode~ to persist everything typed in the minibuffer.

#+begin_src emacs-lisp
  (use-package savehist
    :config
    (setq savehist-file (expand-file-name "hist.el" my-state-directory))
    ;; Auto delete duplicated history.
    (setq history-delete-duplicates t)
    :hook (emacs-startup-hook . savehist-mode))
#+end_src

** Switch buffer with preview

At this point, in addition to memorizing their differences, I'd like to preview them briefly before switching between them.  I use a command named ~consult-buffer~ provide by [[https://github.com/minad/consult][consult.el]] instead of the default ~switch-to-buffer~.

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :commands consult-buffer
    :init
    (keymap-substitute global-map 'switch-to-buffer 'consult-buffer))
#+end_src

** Avaiable actions menu

In one place, I may execute some commands.  Rather than binding all the relevant commands to a series of keystrokes and memorizing it, I prefer to have an interactive menu pop up for me to select it.  So I use [[https://github.com/oantolin/embark][embark.el]] to show actions (commands) relevant to the target around point.  It provides a context menu of commands.

#+begin_src emacs-lisp
  (use-package embark
    :ensure t
    :pin gnu
    :commands
    (embark-act
     embark-dwim)
    :init
    (keymap-set global-map "C-;" 'embark-act)
    (keymap-set global-map "C-'" 'embark-dwim))
#+end_src

This will show all available actions on the current point by pressing ~C-;~,  and call the last action again by pressing ~C-'~.

*** Hide Mode Line in ~embark~ buffers

When I use ~embark-collect~ and ~embark-live~, I hope hide the Mode Line.

#+begin_src emacs-lisp
  (use-package window
    :defer t
    :init
    ;; Hide the mode line of the Embark live/completions buffers
    (add-to-list 'display-buffer-alist
		 '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
		   nil
		   (window-parameters (mode-line-format . none)))))
#+end_src

*** Collect with preview

Automatic preview at point in the Embark Collect buffer.

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :commands consult-preview-at-point-mode)

  (use-package embark
    :ensure embark-consult
    :commands embark-collect-mode
    :hook
    (embark-collect-mode-hook . consult-preview-at-point-mode))
#+end_src

** Help Actions

When I get confused about using Emacs, I use a series of help commands built into Emacs to solve my confusion.  But ... it is not enough.  So I need some better third-party help commands to help me learn about more things.

*** Show avaiable bindings

Sometimes I need get the description of ~embark-act~ bindings.

#+begin_src emacs-lisp
  (use-package embark
    :ensure t
    :pin melpa
    :commands embark-bindings
    :init
    (keymap-set help-map "C-b" 'embark-bindings))
#+end_src

*** Describe keymap

Emacs has a built-in command named ~describe-keymap~, but it is not bind to any key by default.  It is useful, I bind it to ~C-h C-k~.

#+begin_src emacs-lisp
  (use-package help-fns
    :commands describe-keymap
    :init
    (keymap-set help-map "C-k" 'describe-keymap))
#+end_src

*** Search available bindings

Some people use [[https://github.com/justbur/emacs-which-key][which-key.el]], which provides a popup buffer to display all available key bindings.  I used to use it too.  Although it shows bindings as much as possible, I need help finding a command when I have many bindings.  I use ~embark-prefix-help-command~ provides by [[https://github.com/oantolin/embark][embark.el]] to search binding with a ~completing-read~ interface.

#+begin_src emacs-lisp
  (use-package embark
    :ensure t
    :pin gnu
    :commands embark-prefix-help-command)

  (use-package emacs
    :no-require t
    :init
    (setq prefix-help-command #'embark-prefix-help-command))
#+end_src

** Adjust scrolling distance

Emacs will scroll more than half of the screen when I am at the bottom of the buffer and want to continue down.  It causes the focus of my eyes to leave the bottom of the buffer.

#+begin_src emacs-lisp
  (use-package emacs
    :no-require t
    :init
    ;; Automatically adjust ‘window-vscroll’ to view tall lines.
    (setq auto-window-vscroll nil)

    ;; Accelerate scrolling operations
    (setq fast-but-imprecise-scrolling t)

    ;; Kell current position after screen scroll.
    (setq scroll-preserve-screen-position t)

    ;; When at the bottom or top of the buffer, move only one line.
    (setq scroll-margin 0 scroll-conservatively 101)

    ;; Horizontally scrolling threshold.
    (setq hscroll-margin 2 hscroll-step 1))
#+end_src

** Mouse actions

As with most graphics software, Emacs has mouse interactions, some of which are difficult to use and inadequate.  I've tweek them to make the interactions feel more subtle.

** Drag and Drop

#+begin_quote
In computer graphical user interfaces, drag and drop is a pointing device gesture in which the user selects a virtual object by "grabbing" it and dragging it to a different location or onto another virtual object.[fn:2]
#+end_quote

I mostly drag and drop text with mouse.

#+begin_src emacs-lisp
  (use-package mouse
    :config
    (setq mouse-drag-and-drop-region 'control))
#+end_src

** Mouse yank at point

Mouse yank commands yank at point instead of at click.

#+begin_src emacs-lisp
  (use-package mouse
    :config
    (setq mouse-yank-at-point t))
#+end_src

** Search actions

I use [[https://github.com/minad/consult][consult.el]] to improve search commands with preview.

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :commands
    (consult-find
     consult-grep
     consult-keep-lines
     consult-line
     consult-ripgrep
     consult-locate
     consult-git-grep
     consult-focus-lines
     consult-line-multi
     consult-multi-occur)
    :init
    (keymap-set search-map "f" 'consult-find)
    (keymap-set search-map "g" 'consult-grep)
    (keymap-set search-map "k" 'consult-keep-lines)
    (keymap-set search-map "l" 'consult-line)
    (keymap-set search-map "r" 'consult-ripgrep)
    (keymap-set search-map "M-f" 'consult-locate)
    (keymap-set search-map "M-g" 'consult-git-grep)
    (keymap-set search-map "M-k" 'consult-focus-lines)
    (keymap-set search-map "M-l" 'consult-line-multi)
    (keymap-set search-map "M-o" 'consult-multi-occur))
#+end_src

** Goto actions

I also use [[https://github.com/minad/consult][consult.el]] to improve goto commands with preview.

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :commands
    (consult-global-mark
     consult-goto-line
     consult-imenu
     consult-imenu-multi
     consult-mark
     consult-outline)
    :init
    (keymap-substitute goto-map 'goto-line 'consult-goto-line)
    (keymap-substitute goto-map 'imenu 'consult-imenu)
    (keymap-set goto-map "m" 'consult-mark)
    (keymap-set goto-map "o" 'consult-outline)
    (keymap-set goto-map "M-i" 'consult-imenu-multi)
    (keymap-set goto-map "M-m" 'consult-global-mark))
#+end_src

* Use Emacs as general text editor

Emacs is an integrated computing environment, but it also has some features of the modern text editor.  Such as auto-saving, backup, and remote accessing.

And Emacs is a visual editor.  That means that I have a representation of my entire document on your screen, and I can move around freely, editing any part of the document I wish.  So I've also enhanced some visual features of the text.

** Automatic Backups

By default, Emacs saves backup files —those ending in ~— in the current directory, thereby cluttering it up.  Let's place them in a directory follow my [[*File Convention][File Convention]].

I want to keep multiple versions of my backup files to help keep my sanity.  Emacs allow saving an unlimited number of backups, but keeping a backup of five versions is appropriate.

#+begin_src emacs-lisp
  (use-package files
    :defer t
    :config
    ;; Create backup files for each modified file on saving, and enable
    ;; make multiple numbered backup files.
    (setq make-backup-files t
	  version-control t)

    ;; Make backup files using the old file replication.
    (setq backup-by-copying t)

    ;; Adjust the threshold for automatic deletion of versioned backup
    ;; files.
    (setq delete-old-versions t
	  kept-old-versions 5
	  kept-new-versions 5)

    ;; Prevent create backup files in-place, alternative create them in
    ;; `my-data-directory'.
    (setq backup-directory-alist
	  (append `(("."
		     .
		     ,(expand-file-name "backup/" my-data-directory)))
		  backup-directory-alist)))
#+end_src

You may have noticed that I set ~backup-by-copying~ to ~t~, which makes Emacs create backups by copying from the original file.  This differs from the default method, which creates backups by renaming the original file.

** Automatic Saving

By default, Emacs automatically saves your changes to a file intermittently.  If anything should happen, you can recover a file with ~M-x recover-file~.

And Emacs will create the auto-saved file with a name appending *#* to the front and rear of the visited file name in place.  I want to keep the directory clean, and follow my [[*File Convention][File Convention]].  So I push my customized transform for rule of making auto-save file name to ~auto-save-file-name-transforms~.

#+begin_src emacs-lisp
  (use-package files
    :defer t
    :config
    ;; Enable Auto-saving.
    (setq auto-save-default t)

    ;; Disable the message of auto-saving.
    (setq auto-save-no-message t)

    ;; Don't auto-disable auto-save after deleting big chunks.  This
    ;; defeats the purpose of a failsafe.
    (setq auto-save-include-big-deletions t)

    ;; Change default transforms to apply to buffer file name before
    ;; making auto-save file name.
    (setq auto-save-file-name-transforms
	  (append `((".*"
		     ,(expand-file-name "auto-save/" my-data-directory)
		     t))
		  auto-save-file-name-transforms)))
#+end_src

Deleting a substantial portion of the text disables auto-save in the buffer by default.  Thus, if I make many changes and my computer suddenly fails, all my changes will be lost, so save all changes anyway.  For always failsafe to take precedence, I have to set ~auto-save-include-big-deletions~ to ~t~.

After auto-saved files have been created, Emacs will save paths of all auto-saved files to a file named the value of ~auto-save-list-file-name~.  Make it follow my [[*File Convention][File Convention]].

#+begin_src emacs-lisp
  (use-package startup
    :no-require t
    :init
    (setq auto-save-list-file-prefix
	  (expand-file-name "auto-save/session/" my-data-directory)))
#+end_src

** Recent Files

Same as criminals always return to the scene of the crime, I also reopen file from time to time when I am done hacking code or writing articles.  Remembering the path to the file and recalling ~find-file~ is tedious.  Fortunately, Emacs provides *Recentf Mode* to facilitate our return to the crime scene.  It will maintain and persist a list of recent files, I call ~recentf-open~ to get this list after activate *Recentf Mode*.

#+begin_src emacs-lisp :tangle no
  (use-package recentf
    :hook (emacs-startup-hook . recentf-mode))
#+end_src

The list of recented files will store in ~user-emacs-directory~ by default, make it follow my [[*File Convention][File Convention]].  In addition, I use an incredible command named ~consult-recent-file~ provided by [[https://github.com/minad/consult][consult.el]] to give a preview of recent files.

#+begin_src emacs-lisp :tangle no
  (use-package consult
    :ensure t
    :pin gnu
    :commands (consult-recent-file))

  (use-package recentf
    :init
    (setq recentf-save-file
	  (expand-file-name "recent.el" my-state-directory))
    (keymap-set ctl-c-f-map "r" 'consult-recent-file)
    :hook (emacs-startup-hook . recentf-mode))
#+end_src

*Recentf Mode* will print some message in echo area at Emacs startup, like:

#+begin_example
Loading /org/brsvh/bsc/.local/state/emacs/recent.el (source)...done
Cleaning up the recentf list...done (0 removed)
#+end_example

It isn't enjoyable and doesn't help, so I created some advice to stop it from appearing.

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :commands (consult-recent-file))

  (use-package recentf
    :init
    (setq recentf-save-file
	  (expand-file-name "recent.el" my-state-directory))
    (keymap-set ctl-c-f-map "r" 'consult-recent-file)
    :config
    (define-advice recentf-load-list
	(:around (fn &rest args) silence-message)
      "Silencing load message."
      (cl-letf (((symbol-function #'message) #'ignore))
	(apply fn args)))

    (define-advice recentf-cleanup
	(:around (fn &rest args) silence-message)
      "Silencing clean up message."
      (cl-letf (((symbol-function #'message) #'ignore))
	(apply fn args)))
    :hook (emacs-startup-hook . recentf-mode))
#+end_src

** Show basic information of buffer

The basic capabilities of the text editor should include showing basic information of buffer.  For me, they are the number of current line and column, the size of current buffer(file).

#+begin_src emacs-lisp
  (use-package simple
    :hook
    ((emacs-startup-hook . column-number-mode)
     (emacs-startup-hook . line-number-mode)
     (emacs-startup-hook . size-indication-mode)))
#+end_src

** Save the last opened place

After I modified a file, I saved and closed it.  Emacs will go to the top line of the file when next opened.  After I modified a file, I saved and closed it.  Emacs will go to the top line of the file when next opened.  I'm always trying to find where the last revision went.  To avoid this hassle, I used Save Place Mode to save the last place of the file before closing it.

#+begin_src emacs-lisp
  (use-package saveplace
    :config
    (setq save-place-file
	  (expand-file-name "place.el" my-state-directory))
    :hook (emacs-startup-hook . save-place-mode))
#+end_src

Make date storage of /saveplace.el/ follow my [[*File Convention][File Convention]].

** Replace region with new content

Emacs will append the new content to the marked region by default, which goes against operational intuition.

#+begin_src emacs-lisp
  (use-package delsel
    :hook
    ;; Replace content in marked region with new text.
    (emacs-startup-hook . delete-selection-mode))
#+end_src

** Display file name with forward style

When I opened two files that have same name, the buffers of these files will append parent directory name with brackets.  It looks different form the Unix/Linux file path, I preferred to use forward style.

#+begin_src emacs-lisp
  (use-package uniquify
    :init
    ;; Use forward style buffer name.
    (setq uniquify-buffer-name-style 'forward))
#+end_src

** Kill ring and Clipboard

The *kill ring* is a list of blocks of text that were previously killed. And the *clipboard* is the facility that most graphical applications use for /cutting and pasting/.  I would like to make the kill ring of Emacs and the clipboard of system use the same content.  So I need set Emacs save kill ring to clipboard after quit.

#+begin_src emacs-lisp
  (use-package simple
    :config
    ;; Do not saves duplicates in kill-ring
    (setq kill-do-not-save-duplicates t)

    ;; Save clipboard after quit.
    (setq save-interprogram-paste-before-kill t))
#+end_src

I set ~kill-do-not-save-duplicates~ to ~t~, it will remove duplicated content in the kill ring.

* File Management

Emacs can be used as a file manager, in addition to local file management, there is also rich remote protocol support.  I use Emacs as file management client to operate files, and version control.

** Version Control

*Version Control* is is a class of systems responsible for managing changes to computer programs, documents, large web sites, or other collections of information.

Widely used version control software includes *Git*, *Subversion*, *Mercurial*, and etc.

**** Git support

[[https://github.com/magit/magit][magit.el]] is a complete text-based user interface to [[https://git-scm.com/][Git]], it is the world's best Git client.

#+begin_src emacs-lisp
  (use-package magit
    :ensure t
    :pin nongnu
    :commands magit-dispatch
    :init
    (setq magit-define-global-key-bindings nil)
    (keymap-set ctl-c-v-map "g" 'magit-dispatch))
#+end_src

**** Update highlighting view after ~magit~ commit

~diff-hl~ do not refresh fringe after changes have been committed with ~magit~, handle refresh functions of ~diff-hl~ in related ~magit~ hooks.

#+begin_src emacs-lisp
  (use-package magit
    :ensure t
    :pin nongnu
    :defines
    (magit-pre-refresh-hook
     magit-post-refresh-hook))

  (use-package diff-hl
    :ensure t
    :pin gnu
    :hook
    ((magit-pre-refresh-hook . diff-hl-magit-pre-refresh)
     (magit-post-refresh-hook . diff-hl-magit-post-refresh)))
#+end_src

* Life with ~org-mode~

My life can be seen as a combination of mechanical tasks, the planning and finishing of which make up my day.  It's easier to estimate how long a task takes if I keep track of time spent by /clocking in and out/ of tasks.

All my *Org* files stored in the /org/ directory under the ~HOME~ directory.

#+begin_src emacs-lisp
  (use-package org
    :ensure t
    :pin gnu
    :defer t
    :config
    (setq org-directory "~/org"))
#+end_src

** Using ~org-mode~ as a Day Planner

I started realizing my [[https://en.wikipedia.org/wiki/Getting_Things_Done][GTD methodology]] from [[https://emacs.cafe/emacs/orgmode/gtd/2017/06/30/orgmode-gtd.html][Nicolas Pretton's post]] as a starting point.  I call my GTD methodology as *Day Planner*.

My workflow of the day planner with the following steps:

1. Mindlessly capture and collect tasks.
2. Schedule and archive tasks at the end, or start, of the day.
3. Document in detail the tasks I encounter problems or ideas while doing them.
4. Repeat!

I use /org-agenda.el/ to show my tasks by time or category and bind related commands to my customized global key map.

#+begin_src emacs-lisp
  (use-package org-agenda
    :ensure org
    :pin gnu
    :commands org-agenda
    :init
    (keymap-set ctl-c-o-map "a" 'org-agenda))
#+end_src

** Files of Day Planner

I split all my tasks in four separate files:

- /~/org/agenda/inbox.org/: where I collect everything;
- /~/org/agenda/gtd.org/: where I put all my projects;
- /~/org/agenda/someday.org/: All inactive tasks that I might do at some point in the future, but don’t want to see all the time;
- /~/org/agenda/tickler.org/: I put entries in this file with a timestamp to get reminded at the right moment.

And it is necessary to add these files to the agenda file.

#+begin_src emacs-lisp
  (use-package org-agenda
    :ensure org
    :pin gnu
    :defer t
    :config
    (add-to-list 'org-agenda-files "~/org/agenda/inbox.org")
    (add-to-list 'org-agenda-files "~/org/agenda/gtd.org")
    (add-to-list 'org-agenda-files "~/org/agenda/tickler.org"))
#+end_src

My inbox is then processed and emptied daily.  When processing the inbox, I refile each entry that is actionable and belongs to a project using ~C-c C-w~, moving the entry to the appropriate place.  If need be, I create a new project out of it.  These are rules for refile entry.

#+begin_src emacs-lisp
  (use-package org-refile
    :ensure org
    :pin gnu
    :defer t
    :config
    (add-to-list 'org-refile-targets
		 '("~/org/agenda/gtd.org" :maxlevel . 3))
    (add-to-list 'org-refile-targets
		 '("~/org/agenda/someday.org" :level . 1))
    (add-to-list 'org-refile-targets
		 '("~/org/agenda/tickler.org" :maxlevel . 2)))
#+end_src

** Capture and collect tasks

As mentioned above, I use the inbox as a pre-processing place for all tasks.  So I needed a quick way to capture tasks to the inbox, and /org-capture.el/ provided it.

#+begin_src emacs-lisp
  (use-package org-capture
    :ensure org
    :pin gnu
    :commands org-capture
    :init
    (keymap-set ctl-c-o-map "c" 'org-capture)
    :config
    ;; Task.
    (add-to-list 'org-capture-templates
		 '("t"
		   "Todo [inbox]"
		   entry
		   (file+headline "~/org/agenda/inbox.org" "Tasks"))
		 'append)

    ;; Tickler
    (add-to-list 'org-capture-templates
		 '("T"
		   "Tickler"
		   entry
		   (file+headline "~/org/agenda/tickler.org" "Tickler"))
		 'append))
#+end_src

* Prose

Emacs is a writer's best friend, the rich extensions from the Emacs community and the extensibility of Emacs itself offer endless possibilities for writing.

** Generic writing experience improvements for text of Prose

I've thought about what I need from Emacs to support me while working on my literary creations.  They just include *Text Wrap* and *Spell Check* now.

I always use soft wrap when I need to wrap text, and it is *Visual Line Mode* in Emacs to provide this support.

#+begin_src emacs-lisp
  (use-package simple
    :hook
    (text-mode-hook . hl-line-mode))

  (use-package hl-line
    :hook
    (text-mode-hook . visual-line-mode))
#+end_src

As you see, I also need highlight current line when wirte prose.

There are multiple choices for Spell Check in Emacs, /ispell.el/ or /flyspell.el/.

~ispell-word~ is bind to ~M-$~ by default, it is configure free and I don't need setup it.  So I just handle ~flyspell-mode~ to ~text-mode-hook~, it will be available in other writing modes that inherit from ~text-mode~.

#+begin_src emacs-lisp
  (use-package flyspell
    :hook
    (text-mode-hook . flyspell-mode))
#+end_src

*** Wrap Multi-language Text

I am a Chinese native speaker, so I frequently write English and Chinese at the same time.  They will have strange line breaks wrap in Emacs, so it is necessary to set ~word-wrap-by-category~ to get correct word wrap for me.  It is useful for Korean and Japanese too.

#+begin_src emacs-lisp
  (use-package simple
    :config
    (setq word-wrap-by-category t))
#+end_src

** Distraction-free writing

Writing takes total concentration to produce creative prose.  Distractions are the natural enemy of concentration.  While my computer is my most important writing tool, it can also be a source of distractions.

Distraction-free writing means that the computer screen is free of clutter and, just like an old typewriter, only shows the text is working on.

[[https://github.com/rnkn/olivetti][olivetti.el]] is a simple Emacs minor mode that facilities distraction-free writing.  I use it when need full concentration writing in other marjor modes.

#+begin_src emacs-lisp
  (use-package olivetti
    :ensure t
    :pin melpa
    :commands olivetti-mode)
#+end_src

** Write article with ~org-mode~

*Org Mode* is a GNU Emacs major mode for org file format editing, and org file format is markup text language with rich features.  Most of my plain text files are written in org file format.  I export them to other file formats via ~ox~.  Let's tweak it now.

*** Beautify Org Mode

Org mode needs some tweaks to remove some clutter from the screen.  We can change the alignment, change the way fonts of rich text and special characters are displayed and preview images by default.

#+begin_src emacs-lisp
  (use-package org
    :ensure t
    :pin gnu
    :defer t
    :config
    ;; Show entities as UTF8 characters.
    (setq org-pretty-entities t)

    ;;Hide emphasis markers 
    (setq org-hide-emphasis-markers t)

    ;; Show inline image.
    (setq org-startup-with-inline-images t))
#+end_src

Hiding emphasis markers is that rich text becomes hard to edit because it is unclear whether your cursor is on the marker or the first or last character.  The [[https://github.com/awth13/org-appear][org-appear.el]] helps by displaying the markers while the cursor is on a rich text word.

When I'm editing the headline, I adjust the headline level with the ~tab~ key. So ~C-a~ return to before leading star is not appropriate, I always expect to change the first word of the headline instead of changing the level.  Set ~org-special-ctrl-a/e~ to move cursor to the beginning of headline when press ~C-a~.  The same is applied to ~C-e~, which moves to the end of the headline when editing a folded block.

#+begin_src emacs-lisp
  (use-package org
    :ensure t
    :pin gnu
    :defer t
    :config
    (setq org-special-ctrl-a/e t))
#+end_src

#+begin_src emacs-lisp
  (use-package org-appear
    :ensure t
    :pin melpa
    :config
    (setq org-appear-autolinks t)
    :hook
    (org-mode-hook . org-appear-mode))
#+end_src

The asterisk characters used by Org mode to indicate heading levels are practical but look a bit ugly.  The previous configuration already removed the leading stars to reduce clutter.  Next I will replace these symbols with some nice icons.

The [[https://github.com/minad/org-modern][org-modern.el]] improves the look of Org Mode headings by replacing the asterisk symbols with nicer looking circles.  The package also enhances the looks of plain lists, todo items, and tables.

#+begin_src emacs-lisp
  (use-package org-modern
    :ensure t
    :pin gnu
    :config
    ;; Set headline stars use zero width space.
    (setq org-modern-star '("\u200b"))

    ;; Hide all leading stars.
    (setq org-modern-hide-stars t)
    :hook
    (org-mode-hook . org-modern-mode))
#+end_src

Let tags align to the end of headline, it will give a better display.

#+begin_src emacs-lisp
  (use-package org
    :ensure t
    :pin gnu
    :defer t
    :config
    ;; Tags align
    (setq org-auto-align-tags nil
	  org-tags-column 0))
#+end_src

*Org Mode* have some export option for export Org file to other file format.  But I need hide them when writing.

#+begin_src emacs-lisp
  (use-package org
    :ensure t
    :pin gnu
    :config
    (setq org-hidden-keywords '(title
				author
				date
				email
				tags
				options
				export_file_name)))
#+end_src

*** Show Outline Tree

*Org Mode* supports jumping between sections and can also use ~occur~ to match the headline.   However, I still need an intuitive idea of the structure of the current file when writing so-long files.  I use a command ~consult-org-heading~ provide by [[https://github.com/minad/consult][consult.el]] to preview heading and jump into it.

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :commands consult-org-heading)

  (use-package org
    :ensure t
    :pin gnu
    :defer t
    :config
    (keymap-set org-mode-map "M-s M-h" 'consult-org-heading))
#+end_src

*** Distraction-free Org writing

Full concentration to write Org file.

#+begin_src emacs-lisp
  (use-package olivetti
    :ensure t
    :pin melpa
    :hook
    (org-mode-hook . olivetti-mode))
#+end_src

*** Export to other file formats

At some point I want to export my *Org* file to other file formats, such as publish them on the web, or share them with people not using *Org*.  *Org Mode* use /ox.el/ to support for conversion to other files.  But it only activate supports of *HTML* file, *ASCII* file, *Plain* text, *ODT* (Open Document Text) file by default, and I want to get more types support.

**** Markdown

*Markdown* is a popular lightweight markup language for creating formatted text.  It is the default document format that most people use.  So I need easily convert to it to share with the people who use it.

Require /ox-md.el/ to auto extended Markdown support of  ~org-export-dispatch~.

#+begin_src emacs-lisp
  (use-package ox-md
    :ensure org
    :pin gnu
    :after org)
#+end_src

* Programming

Emacs is most powerful programming environment.  But it is required to be configured in detail, otherwise it will be poor.  Let me configure Emacs for all programming languages I use.

** Generic programming enhancement

Display the number of current line and highlight current line when I am programming.

#+begin_src emacs-lisp
  (use-package display-line-numbers
    :hook
    (prog-mode-hook . display-line-numbers-mode))

  (use-package hl-line
    :hook
    (prog-mode-hook . hl-line-mode))
#+end_src

I have already configured corfu to complete text, activate it when I am programming.

#+begin_src emacs-lisp
  (use-package corfu
    :ensure t
    :pin gnu
    :hook
    (prog-mode-hook . corfu-mode))
#+end_src

*** Back to Indentation or Beginning

Most the real code has the indented structure, and Emacs will move to the beginning of line when press ~C-a~ by default.  But most times, I mean move to the beginning of code rather than the line.

#+begin_src emacs-lisp
  (use-package mwim
    :ensure t
    :pin melpa
    :after prog-mode
    :commands
    (mwim-beginning
     mwim-end)
    :init
    (keymap-set prog-mode-map "C-a" 'mwim-beginning)
    (keymap-set prog-mode-map "C-e" 'mwim-end))
#+end_src

*** Emacs with Tree-sitter

#+begin_quote
Tree-sitter is a parser generator tool and an incremental parsing library. It can build a concrete syntax tree for a source file and efficiently update the syntax tree as the source file is edited. [fn:3]
#+end_quote

I use Tree-sitter in Emacs to get faster and fine-grained code highlighting, and more flexible code folding.

Not all programming languages I used support it, so I handle it in the major mode that support it.

#+begin_src emacs-lisp
  (use-package tree-sitter
    :ensure t
    :pin melpa
    :commands tree-sitter-mode)
#+end_src

*** Language Server Protocol

#+begin_quote
The Language Server Protocol (LSP) defines the protocol used between an editor or IDE and a language server that provides language features like auto complete, go to definition, find all references etc. The goal of the Language Server Index Format (LSIF, pronounced like "else if") is to support rich code navigation in development tools or a Web UI without needing a local copy of the source code.[fn:4]
#+end_quote

To get the LSP support, [[https://github.com/emacs-lsp/lsp-mode][lsp-mode.el]] and [[https://github.com/joaotavora/eglot][eglot.el]] both are available for me.  Some people used [[https://github.com/emacs-lsp/lsp-mode][lsp-mode.el]], but I preferred to use [[https://github.com/joaotavora/eglot][eglot.el]].  Because [[https://github.com/joaotavora/eglot][eglot.el]] is considerably less code and hassle than [[https://github.com/emacs-lsp/lsp-mode][lsp-mode.el]].  In most cases, there's nothing to configure. It's a minimalist approach focused on user experience and performance.

And not all programming languages I used need LSP, so I handle it in the major mode that I really need to use.

#+begin_src emacs-lisp
  (use-package eglot
    :ensure t
    :pin gnu
    :commands eglot-ensure
    :config
    ;; Auto kill `eglot' server after kill last modifed buffer.
    (setq eglot-autoshutdown t)

    ;; Remove `eglot' mode line status information.
    (setq mode-line-misc-info
	  (delete `(eglot--managed-mode
		    (" [" eglot--mode-line-format "] "))
		  mode-line-misc-info)))
#+end_src

*** Find definition and references

Use /xref.el/ find definition and references of functions and variables, and get preview feature with [[https://github.com/minad/consult][consult.el]].

#+begin_src emacs-lisp
  (use-package xref
    :defines
    (xref-show-xrefs-function
     xref-show-definitions-function))

  (use-package consult
    :ensure t
    :pin gnu
    :after xref
    :autoload consult-xref
    :init
    (setq xref-show-xrefs-function #'consult-xref
	  xref-show-definitions-function #'consult-xref))
#+end_src

*** On-the-fly syntax checker

Syntax checker checks for syntax errors in each statement, according to the data set type.  And on-the-fly syntax checker show diagnostics while the change affects of code is ongoing.  It help me to write high quality and bug-free code.

I used built-in /flymake.el/ rather than [[https://github.com/flycheck/flycheck][flycheck.el]].

*** Show diagnostics in minions menu

I use *Minions Mode* to hide minor modes I don't want to see, and exception and counters of diagnostics will be hide by default.  Exclude it to display in Minions Menu directly.

#+begin_src emacs-lisp
  (use-package minions
    :ensure t
    :pin melpa
    :defines minions-prominent-modes)

  (use-package flymake
    :defer t
    :after minions
    :config
    (when minions-mode
      (push 'flymake-mode minions-prominent-modes)))
#+end_src

*** Show diagnostics with preview

Use ~completing-read~ to jumping between diagnostics, and get preview features from [[https://github.com/minad/consult][consult.el]].

#+begin_src emacs-lisp
  (use-package consult
    :ensure t
    :pin gnu
    :after flymake
    :commands consult-flymake
    :init
    (keymap-set flymake-mode-map "C-c !" 'consult-flymake))
#+end_src

** C/C++

#+begin_src emacs-lisp
  (use-package tree-sitter
    :ensure t
    :pin melpa
    :hook
    ((c-mode-hook . tree-sitter-mode)
     (c++-mode-hook . tree-sitter-mode)))

  (use-package eglot
    :ensure t
    :pin gnu
    :hook
    ((c-mode-hook . eglot-ensure)
     (c++-mode-hook . eglot-ensure)))
#+end_src

** Emacs Lisp

I use [[https://github.com/mmontone/emacs-inspector][inspector.el]] to inspect the Emacs Lisp objects.

#+begin_src emacs-lisp
  (use-package inspector
    :ensure t
    :pin gnu
    :after elisp-mode
    :commands
    (inspector-inspect-last-sexp
     inspector-inspect-expression)
    :init
    (keymap-set emacs-lisp-mode-map
		"C-c C-i"
		'inspector-inspect-last-sexp)
    (keymap-set emacs-lisp-mode-map
		"C-c M-i"
		'inspector-inspect-expression))
#+end_src

Emacs Lisp Macros enable I to define new control constructs and other language features.  I use it everyday, /pp.el/ is helpful to display pretty expansion of macros.

#+begin_src emacs-lisp
  (use-package pp
    :after elisp-mode
    :commands
    (pp-macroexpand-last-sexp
     pp-macroexpand-expression)
    :init
    (keymap-set emacs-lisp-mode-map
		"C-c <return>"
		'pp-macroexpand-last-sexp)
    (keymap-set emacs-lisp-mode-map
		"C-c M-<return>"
		'pp-macroexpand-expression))
#+end_src

** Haskell

#+begin_src emacs-lisp
  (use-package haskell-mode
    :ensure t
    :pin nongnu
    :commands haskell-mode)

  (use-package tree-sitter
    :ensure t
    :pin melpa
    :hook
    (haskell-mode-hook . tree-sitter-mode))

  (use-package eglot
    :ensure t
    :pin gnu
    :hook
    (haskell-mode-hook . eglot-ensure))
#+end_src

** Rust

#+begin_src emacs-lisp
  (use-package rust-mode
    :ensure t
    :pin nongnu
    :commands rust-mode)

  (use-package tree-sitter
    :ensure t
    :pin melpa
    :hook
    (rust-mode-hook . tree-sitter-mode))

  (use-package eglot
    :ensure t
    :pin gnu
    :hook
    (rust-mode-hook . eglot-ensure))
#+end_src

* Provide Feature :nonexport:

#+begin_src emacs-lisp
  (provide 'brsvh)
  ;;; brsvh.el ends here
#+end_src

* Footnotes

[fn:1]https://github.com/minad/marginalia#marginaliael---marginalia-in-the-minibuffer

[fn:2]https://en.wikipedia.org/wiki/Drag_and_drop

[fn:3]https://tree-sitter.github.io/tree-sitter/

[fn:4]https://microsoft.github.io/language-server-protocol/
